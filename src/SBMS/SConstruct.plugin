
#
#  Jan. 31, 2014  David Lawrence
#
# This SConstruct file can be copied into a directory containing
# the source for a plugin and used to compile it. It will use and
# install into the directory specified by the JLEIC_MY environment
# variable if defined. Otherwise, it will install in the JLEIC_HOME
# directory.
#
# This file should not need modification. It will be copied in by
# the mkplugin or mkfactory_plugin scripts or you can just copy it
# by hand.
#
# To use this, just type "scons install" to build and install.
# Just type "scons" if you want to build, but not install it.
#
# > scons install
#
# Note that unlike the rest of the SBMS system, this does not
# use a separate build directory and builds everything in the
# source directory. This is due to technical details and will hopefully
# be fixed in the future. For now it means, that you must be
# diligent of building for multiple platforms using the same source.
#

import os
import sys
import subprocess
import glob

# Get JLEIC_HOME environment variable, verifying it is set
JLEIC_HOME = os.getenv('JLEIC_HOME')
if(JLEIC_HOME == None):
	print 'JLEIC_HOME environment variable not set!'
	exit(-1)

# Get JLEIC_MY if it exists. Otherwise use JLEIC_HOME
JLEIC_MY = os.getenv('JLEIC_MY', JLEIC_HOME)

# Add SBMS directory to PYTHONPATH
sbmsdir = "%s/src/SBMS" % JLEIC_HOME
sys.path.append(sbmsdir)

import sbms

# Get command-line options
SHOWBUILD = ARGUMENTS.get('SHOWBUILD', 0)

# Get platform-specific name
osname = os.getenv('BMS_OSNAME', 'build')

# Get architecture name
arch = subprocess.Popen(["uname"], stdout=subprocess.PIPE).communicate()[0].strip()

# Setup initial environment
installdir = "%s/%s" %(JLEIC_MY, osname)
include = "%s/include" % (installdir)
bin = "%s/bin" % (installdir)
lib = "%s/lib" % (installdir)
plugins = "%s/plugins" % (installdir)
env = Environment(        ENV = os.environ,  # Bring in full environment, including PATH
                      CPPPATH = [include],
                      LIBPATH = ["%s/%s/lib" %(JLEIC_HOME, osname)],  # n.b. add JLEIC_HOME here and prepend JLEIC_MY below
                  variant_dir = ".%s" % (osname))

# Only add JLEIC_MY library search path if it already exists
# since we'll get a warning otherwise
if (os.path.exists(lib)): env.PrependUnique(lib)

# These are SBMS-specific variables (i.e. not default scons ones)
env.Replace(INSTALLDIR    = installdir,
				OSNAME        = osname,
				INCDIR        = include,
				BINDIR        = bin,
				LIBDIR        = lib,
				PLUGINSDIR    = plugins,
				ALL_SOURCES   = [],        # used so we can add generated sources
				SHOWBUILD     = SHOWBUILD,
		 COMMAND_LINE_TARGETS = COMMAND_LINE_TARGETS)

# Use terse output unless otherwise specified
if SHOWBUILD==0:
	env.Replace(CCCOMSTR       = "Compiling  [$SOURCE]",
				  CXXCOMSTR       = "Compiling  [$SOURCE]",
				  FORTRANPPCOMSTR = "Compiling  [$SOURCE]",
				  FORTRANCOMSTR   = "Compiling  [$SOURCE]",
				  SHCCCOMSTR      = "Compiling  [$SOURCE]",
				  SHCXXCOMSTR     = "Compiling  [$SOURCE]",
				  LINKCOMSTR      = "Linking    [$TARGET]",
				  SHLINKCOMSTR    = "Linking    [$TARGET]",
				  INSTALLSTR      = "Installing [$TARGET]",
				  ARCOMSTR        = "Archiving  [$TARGET]",
				  RANLIBCOMSTR    = "Ranlib     [$TARGET]")


# Get compiler from environment variables (if set)
env.Replace( CXX = os.getenv('CXX', 'g++'),
             CC  = os.getenv('CC' , 'gcc'),
             FC  = os.getenv('FC' , 'gfortran') )

# Add local directory, directories from JLEIC_MY and JLEIC_HOME to include search path
env.PrependUnique(CPPPATH = ['%s/src' % JLEIC_MY, '%s/src/libraries' % JLEIC_MY,     '%s/src/libraries/include' % JLEIC_MY]  )
env.PrependUnique(CPPPATH = ['%s/src' % JLEIC_HOME, '%s/src/libraries' % JLEIC_HOME, '%s/src/libraries/include' % JLEIC_HOME])

env.PrependUnique(CPPPATH = ['%s/%s/include' % (JLEIC_MY,  osname)])
env.PrependUnique(CPPPATH = ['%s/%s/include' % (JLEIC_HOME,osname)])

# Turn on debug symbols and warnings
env.PrependUnique(      CFLAGS = ['-g', '-fPIC', '-Wall'])
env.PrependUnique(    CXXFLAGS = ['-g', '-fPIC', '-Wall'])
env.PrependUnique(FORTRANFLAGS = ['-g', '-fPIC', '-Wall'])

env.PrependUnique(    CXXFLAGS = ['-std=c++11'])

# Apply any platform/architecture specific settings
sbms.ApplyPlatformSpecificSettings(env, arch)
sbms.ApplyPlatformSpecificSettings(env, osname)

# Make plugin from source in this directory
sbms.AddEANA(env)
sbms.AddROOT(env)
sbms.plugin(env)

# Make install target
env.Alias('install', installdir)
